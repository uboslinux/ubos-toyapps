developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=${developer}
pkgname=$(basename $(pwd))
pkgver=0.20
pkgrel=1
pkgdesc="Best Guestbook Evar, in Java this time, using MySQL"
_groupId='net.ubos.ubos-toyapps'
arch=('any')
makedepends=('maven' 'jdk11-openjdk' )
depends=('java-runtime=11' 'diet4j' 'mysql-connector-java')
license=('AGPL3')
options=('!strip')
_java_home=/usr/lib/jvm/java-11-openjdk

prepare() {
    cd ${startdir}/maven
    sed -e "s/PKGBUILD_VERSION/${pkgver}/" pom.xml.tmpl > pom.xml
}

build() {
    cd ${startdir}/maven
    JAVA_HOME=${_java_home} mvn clean install ${MVN_OPTS}
}

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/
    install -D -m0644 ${startdir}/appicons/license.txt         -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Code and pom file
    install -D -m0644 ${startdir}/maven/target/${pkgname}-${pkgver}.war \
                      ${pkgdir}/ubos/lib/java/${_groupId//.//}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.war
    install -D -m0644 ${startdir}/maven/pom.xml \
                      ${pkgdir}/ubos/lib/java/${_groupId//.//}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.pom

# Templates, with package/module names and versions replaced
    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

    groupId=${_groupId//./\\/}

    sed -i -e "s/\${pkgname}/${pkgname}/g" ${pkgdir}/ubos/share/${pkgname}/tmpl/context.xml.tmpl
    sed -i -e "s/\${pkgver}/${pkgver}/g"   ${pkgdir}/ubos/share/${pkgname}/tmpl/context.xml.tmpl
    sed -i -e "s/\${groupId}/${groupId}/g" ${pkgdir}/ubos/share/${pkgname}/tmpl/context.xml.tmpl

    install -D -m0644 ${startdir}/sql/create.sql -t ${pkgdir}/ubos/share/${pkgname}/sql/
}
